/*-*- mode:unix-assembly; indent-tabs-mode:t; tab-width:8; coding:utf-8     -*-â”‚
â”‚vi: set et ft=asm ts=8 tw=8 fenc=utf-8                                     :viâ”‚
â•žâ•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•¡
â”‚ Copyright 2020 Justine Alexandra Roberts Tunney                              â”‚
â”‚                                                                              â”‚
â”‚ Permission to use, copy, modify, and/or distribute this software for         â”‚
â”‚ any purpose with or without fee is hereby granted, provided that the         â”‚
â”‚ above copyright notice and this permission notice appear in all copies.      â”‚
â”‚                                                                              â”‚
â”‚ THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL                â”‚
â”‚ WARRANTIES WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED                â”‚
â”‚ WARRANTIES OF MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE             â”‚
â”‚ AUTHOR BE LIABLE FOR ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL         â”‚
â”‚ DAMAGES OR ANY DAMAGES WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR        â”‚
â”‚ PROFITS, WHETHER IN AN ACTION OF CONTRACT, NEGLIGENCE OR OTHER               â”‚
â”‚ TORTIOUS ACTION, ARISING OUT OF OR IN CONNECTION WITH THE USE OR             â”‚
â”‚ PERFORMANCE OF THIS SOFTWARE.                                                â”‚
â•šâ”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€*/
#include "ape/lib/pc.h"
#include "libc/macros.h"

/	Rounds to nearest integer, away from zero.
/
/	@param	ð‘¥ is an 80-bit long double passed on stack in 16-bytes
/	@return	result of exponentiation on FPU stack in %st
tinymath_roundl:
	push	%rbp
	mov	%rsp,%rbp
	.profilable
	push	%rax
	fldt	16(%rbp)
	fnstcw	-6(%rbp)
	fnstcw	-8(%rbp)
	orb	$0b00001100,-7(%rbp)		# RC = â†’0
	fxam					# C1 is set to sign bit
	fnstsw
	fabs
	test	$FPU_C1>>8,%ah
	fadds	.Lhalf(%rip)
	fldcw	-8(%rbp)
	frndint
	fldcw	-6(%rbp)
	je	1f
	fchs
1:	leave
	ret
	.endfn	tinymath_roundl,globl
	.alias	tinymath_roundl,roundl
	.source	__FILE__

	.rodata.cst4
.Lhalf:	.float	.5
